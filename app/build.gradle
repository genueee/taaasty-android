buildscript {
    repositories {
        mavenCentral()
        maven { url 'https://www.testfairy.com/maven' }
    }

    dependencies {
        classpath 'com.testfairy.plugins.gradle:testfairy:1.+'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'testfairy'

ext.private = new Properties()
if (rootProject.file('private.properties').exists()) {
    ext.private.load(new FileInputStream(rootProject.file('private.properties')));
}

def getRevision = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

repositories {
    maven { url "http://maven.aviary.com/repo/release" }
    maven { url "http://clojars.org/repo" }
    flatDir {
        dirs 'libs'
    }
}

android {
    compileSdkVersion 22
    buildToolsVersion "22"

    defaultConfig {
        applicationId "ru.taaasty"
        minSdkVersion 15
        targetSdkVersion 22
        versionCode 12
        versionName "1.0.2"

        buildConfigField "String", "API_SERVER_ADDRESS",
                project.ext.private.getProperty("default.ApiServerAddress",
                        "\"http://api.taaasty.com\"")
        buildConfigField "String", "API_SERVER_LOGIN",
                project.ext.private.getProperty("default.ApiServerLogin",
                        "\"\"")
        buildConfigField "String", "API_SERVER_PASSWORD",
                project.ext.private.getProperty("default.ApiServePassword",
                        "\"\"")
        buildConfigField "String", "VKONTAKTE_APP_ID",
                project.ext.private.getProperty("default.VkontakteAppId", "\"4553661\"")
        buildConfigField "String", "YOUTUBE_APP_ID",
                project.ext.private.getProperty("default.YoutubeAppId", "\"AIzaSyBP-gucjXR3BVlVCKQtNzFAdBPy3Tw3_E4\"")
        buildConfigField "String", "THUMBOR_SERVER",
                project.ext.private.getProperty("default.ThumborServer", "\"http://thumbor0.tasty0.ru\"")
        buildConfigField "String", "THUMBOR_KEY",
                project.ext.private.getProperty("default.ThumborKey", "\"golobog\"")
        buildConfigField "String", "PUSHER_KEY",
                project.ext.private.getProperty("default.PusherKey", "\"40dbf1d864d4b366b5e6\"")
        buildConfigField "String", "PUSHER_KEY",
                project.ext.private.getProperty("default.PusherKey", "\"40dbf1d864d4b366b5e6\"")
        buildConfigField "String", "INTERCOM_API_KEY",
                project.ext.private.getProperty("default.IntercomApiKey", "\"android_sdk-fdc4087976c13ad0c63f2aa12c850b81a43f5184\"")
        buildConfigField "String", "INTERCOM_APP_ID",
                project.ext.private.getProperty("default.IntercomAppId", "\"ef4ce706c805fcdd3cb11255f0c3d8b41f46ba15\"")
        buildConfigField "String", "GOOGLE_APP_ID",
                project.ext.private.getProperty("default.GoogleAppId", "\"73588603634\"")

        resValue "string", "aviary_key", "fe37f3b5e87c67a2"
        resValue "string", "aviary_secret", "3ef989608e94cdc5"
        resValue "string", "facebook_app_id", "\"731919696901929\""

        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_7
            targetCompatibility JavaVersion.VERSION_1_7
        }
    }

    signingConfigs {
        debug {
            storeFile file('../taaasty.debug.keystore')
            storePassword 'android'
            keyAlias 'debug'
            keyPassword 'android'
        }
        release
        beta
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "dbg-" + getRevision()
            resValue "string", "app_name", "Тейсти dbg" + getRevision()
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro', 'proguard-aviary.pro'
            signingConfig signingConfigs.release
        }
        beta {
            applicationIdSuffix ".beta";
            versionNameSuffix "b-" + getRevision()
            resValue "string", "app_name", "Тейсти beta"
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro', 'proguard-aviary.pro'
            signingConfig signingConfigs.beta
        }
    }

    lintOptions {
        warning 'InvalidPackage'
        disable 'RtlHardcoded'
        disable 'AppCompatResource'
    }

    testfairyConfig {
        apiKey "d4c4e1c7be190a08909ce36747839e2ca45e74a2"
        autoUpdate true
        notify true
        iconWatermark true
        video "off";
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile project(':ndkstackblur')
    compile project(':ViewPagerIndicator')

    // You must install or update the Support Repository through the SDK manager to use this dependency.
    compile 'com.android.support:support-annotations:22.0.0'
    compile 'com.android.support:support-v13:22.0.0'
    compile 'com.android.support:support-v4:22.0.0'
    compile 'com.android.support:recyclerview-v7:21.0.3'
    compile 'com.facebook.android:facebook-android-sdk:3.22.0'
    compile 'com.github.chrisbanes.photoview:library:1.2.3'
    compile 'com.google.android.gms:play-services-analytics:7.0.0'
    compile 'com.google.android.gms:play-services-gcm:7.0.0'
    compile 'io.reactivex:rxandroid:0.24.0'
    compile 'com.pusher:pusher-java-client:0.3.3'
    compile 'com.squareup.okhttp:okhttp:2.2.0'
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.2.0' // aviary
    compile 'com.squareup:pollexor:2.0.2'
    compile 'com.squareup.picasso:picasso:2.5.0'
    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile 'com.vk:androidsdk:1.3.2'
    compile 'de.greenrobot:eventbus:2.4.0'
    compile 'javax.annotation:jsr250-api:1.0'
    compile 'pl.droidsonroids.gif:android-gif-drawable:1.1.2'
    compile 'uk.co.chrisjenx:calligraphy:1.1.+'
    compile(group: 'com.aviary.android.feather.sdk', name: 'aviary-sdk', version: '3.4.3.351') {
        exclude(group: 'com.squareup.okhttp', module: 'okhttp')
        exclude(group: 'com.squareup.okhttp', module: 'okhttp-urlconnection')
        exclude(group: 'com.intellij', module: 'annotations')
        exclude(group: 'org.apache.commons', module: 'commons-lang3')
        exclude(group: 'commons-io', module: 'commons-io')
    }
    compile(group: 'it.sephiroth.android.library.picasso', name: 'picasso', version: '2.3.4.3') {
        exclude(group: 'com.squareup.okhttp', module: 'okhttp')
        exclude(group: 'com.squareup.okhttp', module: 'okhttp-urlconnection')
        exclude(group: 'com.intellij', module: 'annotations')
        exclude(group: 'commons-io', module: 'commons-io')
    }

    // intercom
    compile(name:'intercomsdk-0.8.5', ext:'aar')
    //compile 'com.mcxiaoke.volley:library:1.0.9'
    compile 'com.google.code.gson:gson:2.3.1'
    compile 'com.android.support:appcompat-v7:22.0.0'

    testCompile 'junit:junit:4.12'
}

configurations.all {
    resolutionStrategy {
        eachDependency { DependencyResolveDetails details ->
            if (details.requested.group == 'com.aviary.android.feather.sdk' && details.requested.name == 'common-library') {
                details.useVersion '3.1.2'
            }
        }
    }
}

['beta', 'release'].each { typeName ->
    def bt = android.buildTypes[typeName]
    bt.buildConfigField "String", "API_SERVER_ADDRESS",
            project.ext.private.getProperty("${typeName}.ApiServerAddress", "\"http://api.taaasty.com\"")
    bt.buildConfigField "String", "API_SERVER_LOGIN",
            project.ext.private.getProperty("${typeName}.ApiServerLogin", "\"\"")
    bt.buildConfigField "String", "API_SERVER_PASSWORD",
            project.ext.private.getProperty("${typeName}.ApiServerPassword", "\"\"")
    bt.buildConfigField "String", "VKONTAKTE_APP_ID",
            project.ext.private.getProperty("${typeName}.VkontakteAppId", "\"4553661\"")
    bt.buildConfigField "String", "THUMBOR_KEY",
            project.ext.private.getProperty("${typeName}.ThumborKey", "\"golobog\"")
    bt.buildConfigField "String", "THUMBOR_SERVER",
            project.ext.private.getProperty("${typeName}.ThumborServer", "\"http://thumbor0.tasty0.ru\"")
    bt.buildConfigField "String", "YOUTUBE_APP_ID",
            project.ext.private.getProperty("${typeName}.YoutubeAppId", "\"AIzaSyBP-gucjXR3BVlVCKQtNzFAdBPy3Tw3_E4\"")
    bt.buildConfigField "String", "PUSHER_KEY",
            project.ext.private.getProperty("${typeName}.PusherKey", "\"40dbf1d864d4b366b5e6\"")
    bt.buildConfigField "String", "INTERCOM_API_KEY",
            project.ext.private.getProperty("${typeName}.IntercomApiKey", "\"android_sdk-fdc4087976c13ad0c63f2aa12c850b81a43f5184\"")
    bt.buildConfigField "String", "INTERCOM_APP_ID",
            project.ext.private.getProperty("${typeName}.IntercomAppId", "\"ef4ce706c805fcdd3cb11255f0c3d8b41f46ba15\"")
    bt.buildConfigField "String", "GOOGLE_APP_ID",
            project.ext.private.getProperty("${typeName}.GoogleAppId", "\"73588603634\"")
}

if (ext.private.getProperty('release.StoreFile') != null) {
    android.signingConfigs.release.storeFile = file(ext.private['release.StoreFile'])
    android.signingConfigs.release.storePassword = ext.private['release.StorePassword']
    android.signingConfigs.release.keyAlias = ext.private['release.KeyAlias']
    android.signingConfigs.release.keyPassword = ext.private['release.KeyPassword']
} else {
    android.buildTypes.release.signingConfig = null
}

if (ext.private.getProperty('beta.StoreFile') != null) {
    android.signingConfigs.beta.storeFile = file(ext.private['beta.StoreFile'])
    android.signingConfigs.beta.storePassword = ext.private['beta.StorePassword']
    android.signingConfigs.beta.keyAlias = ext.private['beta.KeyAlias']
    android.signingConfigs.beta.keyPassword = ext.private['beta.KeyPassword']
} else {
    android.buildTypes.beta.signingConfig = null
}
